#!/usr/bin/perl -w
# dh_installtex --- register Type 1 fonts, languages, or formats with TeX
# Copyright (C) 2006, 2007 Florent Rougon
# Copyright (C) 2006, 2007 Frank KÃ¼ster
# Copyright (C) 2006-2017 Norbert Preining
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; version 2 dated June, 1991.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; see the file COPYING. If not, write to the
# Free Software Foundation, Inc., 51 Franklin St, Fifth Floor,
# Boston, MA  02110-1301 USA.
#


use strict;
use Debian::Debhelper::Dh_Lib;

our $VERSION = "1";

init(options => { "flavor=s" => \$dh{FLAVOR} });




#
# GLOBAL VARIABLES
#
my $flavor;		
my $mapdoconfig =  0;	  # doconfig depending map/subflavor
my $doallformats = 0;	  # depending on flavor format:build_all
my $dofulllsr = 0;        # do include texmf-texlive in lsr making
my @addbuildformats = (); # additional formats to be build
my $doformatlinks = 1;
my $texmftree = "texmf";
my %cmdlineargs;
my %cmdlinefiles;

#
# definitions for the different flavors
#
# extension of package files debian/$package.formats and/or debian/formats etc
my %pkgfileext = ( 
	hyphen 		=> "hyphens", 
	format 		=> "formats", 
	map 		=> "maps" );
# extension of the config files
my %configfileext = ( 
	hyphen	 	=> "cnf", 
	format 		=> "cnf", 
	map 		=> "cfg" );
# comment char for magic header
my %commentchar = ( 
	hyphen	 	=> '%', 
	format 		=> '#', 
	map 		=> '#' );
# directory under /etc/texmf/ where files are installed
my %configdir = ( 
	hyphen	 	=> "hyphen.d", 
	format 		=> "fmt.d", 
	map 		=> "updmap.d" );
# directory under /var/lib/tex-common where list files are installed
my %managedir = ( 
	hyphen	 	=> "hyphen-cnf", 
	format 		=> "fmtutil-cnf", 
	map 		=> "fontmap-cfg" );
#
# dummy loop variables
my $i;
my $bn;
my $pr;
my $dofilen;

#
# collect data
# This function is used to extract certain information from the respective
# config files. Up to now we have to do the following:
# for config file for maps
# 	if we select to generate all dvips config files
# 	(with map:config_for_active_maps or map:config_for_all_maps)
# 	we collect the map files
# for config file for formats
# 	we extract format\tengine for
# 		- generation with fmtutil-sys --byfmt
# 		- creation of links /usr/bin/format -> /usr/bin/engine
sub collect_data {
	my ($type,$dataref,$entry) = @_;
	my $m;

	if ($type eq "map") {
		if ($m = extract_map($entry, $mapdoconfig)) { push @$dataref, $m; }
	} elsif ($type eq "format") {
		if ($m = extract_format($entry)) { push @$dataref, $m; }
	} 
	# TODO should we do something with hypehn here!????
}

sub extract_format {
	my ($line) = @_;
	if ($line =~ m/^([^#\s]\S+)\s*(\S*)\s+/) {
		return "$1\t$2";
	}
}

sub extract_map {
	my ($line,$mapdoconfig) = @_;
	if ($mapdoconfig == 0) { return ""; }
	if ($line =~ m/^[[:space:]]*([#[:space:]]*)[[:space:]]*(Mixed|Kanji)?Map[[:space:]]*(.*\.map)[[:space:]]*(#.*)?$/) {
		my $comment = $1;
		my $map = $3;
		if (($comment eq "") || ($mapdoconfig == 2)) {
			return $map;
		}
	}
	return "";
}

#
# build_line
# used to create a correct config file entry from a cmd-line specification
sub build_line {
	my ($type,$line) = @_;
	if ($type eq "map") {
		if ($line =~ m/^(Map|MixedMap|KanjiMap),(.*)$/) {
			return("$1 $2");
		} else {
			error("$line is neither of the form Map filename.map, nor MixedMap filename.map.");
		}
	} elsif ($type eq "format") {
		my ($format,$engine,$pat,@rest) = split(",",$line);
		my $ret = "$format\t$engine\t$pat\t" . join(",",@rest);
		return($ret);
	} elsif ($type eq "hyphen") {
		my ($lang,$loader,$pat,@rest) = split(",",$line);
		my $ret="name=$lang file=$loader file_patterns=$pat";
		my @synonyms = ();
		my $lhm;
		my $rhm;
		my $exceptions;
		foreach (@rest) {
			my ($a,$b) = split("=", $_, 2);
			if ($a eq "lhm") {
				$lhm = $b;
			} elsif ($a eq "rhm") {
				$rhm = $b;
			} elsif ($a eq "synonym") {
				push @synonyms, $b;
			} elsif ($a eq "exceptions") {
				$exceptions = $b;
			} else {
				error("$line is not well formed.");
			}
		}
		$ret .= " lefthyphenmin=$lhm" if defined($lhm);
		$ret .= " righthyphenmin=$rhm" if defined($rhm);
		$ret .= " file_exceptions=$exceptions" if defined($exceptions);
		if ($#synonyms >= 0) {
			$ret .= " synonyms=" . join(",",@synonyms);
		}
		$ret .= "\n";
		return($ret);
	}
}

#
#
# START OF THE MAIN PROGRAM
#
#

#
# parse the cmd line and fill in the various hashes
#
foreach (@ARGV) {
	if ((m/^(map)file=(.*\.cfg)$/) ||
		(m/^(hyphen)file=(.*\.cnf)$/) ||
		(m/^(format)file=(.*\.cnf)$/)) {
		my $type=$1;
		my $fn=$2;
		$cmdlinefiles{$type}{$fn} = 1;
	} elsif (m/^(map|hyphen|format)=(.*)$/) {
		push @{$cmdlineargs{$1}}, $2;
	} else {
		error("Unrecognized argument: $_\n");
	}
}

#
#
if (defined($dh{FLAVOR})) {
	foreach my $fl (split (/,/,$dh{FLAVOR})) {
		if ($fl eq "map:config_for_active_maps") {
			$mapdoconfig = 1;
		} elsif ($fl eq "map:config_for_all_maps") {
			$mapdoconfig = 2;
		} elsif ($fl eq "format:build_all") {
			$doallformats = 1;
		} elsif ($fl =~ /^format:add_one:(.*)$/) {
			push @addbuildformats , $1;
		} elsif ($fl eq "format:no_links") {
			$doformatlinks = 0;
		} elsif ($fl eq "lsr:full") {
			$dofulllsr = 1;
		} elsif ($fl eq "tree:texlive") {
			$texmftree = "texlive";
		} else {
			error("Specified flavor $fl not supported.\nPlease see man page for supported flavors!\n");
		}
	}
}



foreach my $package (@{$dh{DOPACKAGES}}) {
	# these variables should be local to the loop over packages
	# as they vary with package
	my $tmp=tmpdir($package);
	my %pkgprovidedfilecontents;
	my %data;
	my @whattodo = ();

	if (($package eq $dh{FIRSTPACKAGE} || $dh{PARAMS_ALL}) && @ARGV) {
		# we do nothing here
	} else {
		# we have to clear all the cmd line arguments!
		%cmdlinefiles = ();
		%cmdlineargs = ();
	}

	foreach my $type ("map", "hyphen", "format") {
		my @cmdlineconfigfiles = ();
		my @cmdlinearguments = ();
		my $pkgprovidedfile = 0;
		my $pkgfileoncmdline = 0;
		my @listlines;
		my $pkgfilename = pkgfile($package,$pkgfileext{$type});
		if ($pkgfilename) {
			open(FOO, "<$pkgfilename") || error("$pkgfilename cannot be opened.");
			my @bar = <FOO>;
			close(FOO);
			$pkgprovidedfilecontents{$type} = \@bar;
			$pkgprovidedfile = 1;
		}
		if (defined($cmdlinefiles{$type})) {
			@cmdlineconfigfiles = keys(%{$cmdlinefiles{$type}});
		}

		if (defined($cmdlineargs{$type})) {
			@cmdlinearguments = @{$cmdlineargs{$type}};
		}
		foreach my $foo (@cmdlineconfigfiles) {
			my $bn=basename($foo);
			if ($bn eq "$package.$configfileext{$type}") {
				$pkgfileoncmdline = 1;
			}
		}
		if (!$pkgprovidedfile && ($#cmdlineconfigfiles < 0) && ($#cmdlinearguments < 0)) {
			# we have nothing to do here, skip to the next one!
			next;
		}
		# we got something, either a cmd line are for one of the
		# config files, or a package (e.g., debian/pkg.maps) file 
		# do the full maintainer stuff!
		push @whattodo, $type;
		if ($pkgfileoncmdline && ($pkgprovidedfile || ($#cmdlinearguments >= 0))) {
			error("This call would create multiple copies of $package.$configfileext{$_}.\nPlease read the man page on how this should be fixed!\n");
		}

		my $target = "/var/lib/tex-common/$managedir{$type}/$texmftree";
		if ( ! $dh{ONLYSCRIPTS} && ! -d "$tmp$target") {
			doit("install", "-d", "$tmp$target");
		};
		#
		# the cmd line cfg files
		#
		foreach (@cmdlineconfigfiles) {
			$bn=basename($_);
			$dofilen = "$tmp$target/$bn";
			WRITECMDLINECFGFILE: do {
				-r $dofilen &&
					error("The config file $dofilen already exists! Cannot recreate it, please call dh_clean -k!");
				open(CFGFILE, ">$dofilen") ||
					error("Cannot open $dofilen for writing!");
				verbose_print("Writing $dofilen");
				open(FOO,"<$_") || error("Cannot open $_ for reading!");
				while (<FOO>) { 
					print CFGFILE $_; 
					collect_data($type,\@{$data{$type}},$_);
				}
				close(FOO);
				close(CFGFILE);
				$bn =~ s/\.$configfileext{$type}$//;
			} unless ($dh{ONLYSCRIPTS});
		}
		#
		# now debian/package.maps and/or debian/maps formats languages
		# merge in the cmd line arguments
		#
		if ( ! $dh{ONLYSCRIPTS} && ( $pkgprovidedfile || ($#cmdlinearguments >= 0) )) {
			my $basefile = "$package.$configfileext{$type}";
			$dofilen = "$tmp$target/$basefile";
			-r $dofilen && 
				error("The config file $dofilen already exists! Cannot recreate it, please call dh_clean -k!");
			open(CFGFILE, ">$dofilen") || 
				error("Cannot open $dofilen for writing!");
			verbose_print("Writing $dofilen");
			foreach (@{$pkgprovidedfilecontents{$type}}) {
				print CFGFILE "$_";
				collect_data($type,\@{$data{$type}},$_);
			}
			foreach (@cmdlinearguments) {
				my $foo =  build_line($type,$_);
				print CFGFILE $foo,"\n";
				collect_data($type,\@{$data{$type}},$foo);
			}
			close(CFGFILE);
		};

	}
	
	my @mapdata = ();
	if (defined($data{"map"})) {
		@mapdata = @{$data{"map"}};
	}
	if ( ! $dh{ONLYSCRIPTS} && ($#mapdata >= 0) ) {
		doit("install","-d","$tmp/usr/share/texmf/dvips/config/");
	};
	INSTALLDVIPSCONF: do {
		foreach $i (@mapdata) {
			my $font = $i;
			$font =~ s/\.map$//;
			$dofilen = "$tmp/usr/share/texmf/dvips/config/config.$font";
			-r $dofilen &&
				error("The dvips config file $dofilen already exists!\nYou may have to call dh_clean -k!\n");
			open(CNFFILE, ">$dofilen") ||
				error("Cannot open $dofilen for writing!");
			verbose_print("Writing $dofilen");
			print CNFFILE "p +$i\n";
			close(CNFFILE);
		} 
	} unless ($dh{ONLYSCRIPTS});

	my @fmtpairs = ();
	my @fmtdata = ();
	my @postrmfmtdata = ();
	if (defined($data{"format"})) {
		@fmtpairs = @{$data{"format"}};
	}
 	installformatlink: do {
		foreach my $pair (@fmtpairs) {
			my ($format,$engine) = $pair =~ m/^(.*)\t(.*)$/;
			if ($engine =~ m/^(mf|mf-nowin)$/) {
				push @postrmfmtdata, "metafont/$format";
			} elsif ($engine eq "mpost") {
				push @postrmfmtdata, "metapost/$format";
			} else {
				push @postrmfmtdata, "$engine/$format";
			}
			push @postrmfmtdata, "$format";
			push @fmtdata, "$format";
			if ($doformatlinks && ($format ne $engine)) {
				my $formatlink = "$tmp/usr/bin/$format";
				if ( -l $formatlink ) {
					if (readlink($formatlink) eq $engine) {
						# correct link generated, only give message
						warning ("/usr/bin/$format link already correctly created, skipping recreation");
					} else {
						# it is a link but pointing to something different
						# error out
						error ("/usr/bin/$format link points to something different but $engine!");
					}
				} elsif ( -e $formatlink ) {
					error ("/usr/bin/$format already exists and not a link, exiting");
				} else {
					doit("mkdir","-p","$tmp/usr/bin");
					doit("ln","-s","$engine","$formatlink");
		  		};
			}
		} 
	} unless ($dh{ONLYSCRIPTS});

	if ($#addbuildformats >= 0) {
		push @fmtdata, @addbuildformats;
		push @postrmfmtdata, @addbuildformats;
	}
	if (! $dh{NOSCRIPTS}) {
		# reproducible builds: sort the two arrays!
		@whattodo = sort @whattodo;
		@postrmfmtdata = sort @postrmfmtdata;
		my $whattodoinst = "update-texmf-config @whattodo";
		my $whattodorem  = "dhit_call_update_texmf_config @whattodo";
		autoscript($package, "postinst", "postinst-tex", "s|#WHATTODO#|$whattodoinst|");
		# map entries have already been filtered out
		autoscript($package, "postrm",   "postrm-tex", "s|#FORMATS#|@postrmfmtdata|; s|#WHATTODO#|$whattodorem|");
	}

	addsubstvar($package, "misc:Depends", "tex-common", ">= 6.13");
}

=head1 NAME

dh_installtex - register Type 1 fonts, hyphenation patterns, or formats with TeX

=head1 SYNOPSIS

B<dh_installtex>
[S<I<debhelper options>>]
[B<-n>]
[B<--flavor=>I<flavor>[,I<flavor>]]
[B<map=MixedMap,>I<file.map>]
[B<map=Map,>I<file.map>]
[B<map=KanjiMap,>I<file.map>]
[B<mapfile=>I<file.cfg>]
[B<hyphen=>I<lang>,I<file>[,lhm=I<n>][,rhm=I<n>][,synonym=I<syn>,...]]
[B<hyphenfile=>I<file.cnf>]
[B<format=>I<format>,I<engine>,I<hyphenfile>,I<rest args>]
[B<formatfile=>I<file.cnf>]

=head1 DESCRIPTION

dh_installtex is a debhelper program that is responsible for
updating the ls-R databases, registering map files, new formats, 
and new hyphenation patterns with TeX.

Your package should depend on an appropriate version of tex-common so
that the update-* commands are available. (This program adds that
dependency to ${misc:Depends}.)

B<WARNING> This program does B<not> check for the actual existence of any 
input files in TEXMF trees. If you call it without any further specification
on which packages to work on (using C<-p> or C<-N>) it will add calls to
management functions to B<all> packages. In this case don't forget to 
add ${misc:Depends} to all packages' dependencies.

=head1 SIMPLE USAGE: Registering Files 

If you only install files into /usr/share/texmf, nothing has to be done,
tex-common will automatically detect changes and run mktexlsr.

=head1 COMPLEX USAGE: Registering fonts, hyphenation patterns and formats

If in addition, you have to install map files, hyphenation patterns for
additional languages, or format definitions you can use three
different methods to specify what should be installed:

1) B<Pre made config files:> These files can be specified with the
different I<type>file= options.
I<type> can be one of map, format and hyphen, and these files will be
installed into /var/lib/tex-common/I<configdir>/ for the respective type 
(fontmap-cfg for map files, hyphen-cnf for hyphenation patterns,
and fmtutil-cnf for format definitions).

Example:

        dh_installtex mapfile=foo.cfg formatfile=debian/bar.cnf

would install foo.cfg as /var/lib/tex-common/fontmap-cfg/texmf/foo.cfg, 
and debian/bar.cnf as /var/lib/tex-common/fmtutil-cnf/texmf/bar.cnf.


2) B<Directly on the cmd line:>
You can specify maps, formats, and hyphenations on the cmd line. The items
are stored in the respective config file 20package

Example:

        dh_installtex map=Map,foo.map hyphen=ngerman-x-2011-07-01,dehyphn-x-2011-07-01.tex,hyph-de-1996.pat.txt,lhm=2,rhm=2,synonym=ngerman-x-latest,exceptions=hyph-de-1996.hyp.txt

would install a file /var/lib/tex-common/fontmap-cfg/texmf/package.cfg containing the line

        Map foo.map

and a file /var/lib/tex-common/hyphen-cnf/texmf/package.cnf containing the lines

    name=ngerman-x-2011-07-01 file=dehyphn-x-2011-07-01.tex file_patterns=hyph-de-1996.pat.txt lefthyphenmin=2 righthyphenmin=2 synonym=ngerman-x-latest file_exceptions=hyph-de-1996.hyp.txt

3) B<Package files:>
You create a file debian/package.maps or debian/maps,
debian/package.hyphens or debian/hyphens, 
debian/package.formats or debian/formats. These files are
installed with the name of the package. Each of these
files will be installed into the first package dh_installtex
is told to act on. By default this is the first binary package in
debian/control, but if you use -p, -i, or -a flags, it will be the first
package specified by those flags.

Example:

        dh_installtex

would install a present debian/package.formats file as
/var/lib/tex-common/fmtutil-cnf/texmf/package.cnf.


=head2 Mixing the different variants

The command line items (Variant 2) are merged into the debian/package.maps 
(debian/package.hyphens, debian/package.formats) file. If you specify
an additional package.cfg/cnf (Variant 1) this will raise an error since
both files would be installed as package.cfg/cnf.

=head1 OPTIONS

=over 4

=item B<-n>, B<--noscripts>

Do not modify postinst/prerm scripts.

=item B<--flavor=>I<flavor>[,I<flavor>]

This option is used to select a different tree then the default
/usr/share/texmf, and to switch on additional options. The argument
specify a list from flavors to be selected.
At the moment you can select from the following list of flavors:
B<tree:texlive>,
B<map:config_for_active_maps>, B<map:config_for_all_maps>, 
B<format:add_one:I<formatname>>.

B<tree:texlive> will select the tree /usr/share/texlive/texmf-dist and
should only be used for files installed into this tree.

B<map:config_for_active_maps> will create a file I<config.bar> for each active
(i.e. uncommented) map in each of the cfg file generated by one of the
three methods described above. These files are installed in
/usr/share/texmf/dvips/config/.

If you select B<map:config_for_all_maps> the script will generate I<config.bar>
even for those map files which are present in a cfg file, but deactivated by a comment.

The file I<config.bar> is used when called by `dvips -Pbar ...'. Thus it
allows the activation of single map files even if they are not automatically
activated via the updmap(-sys) mechanism.

Default is I<not> to generate any config files.

B<format:add_one:I<formatname>> allows to add the generation of a specific
format without actually providing it (in a cnf file).  This is useful if a 
package adds hyphenation patterns to an existing formats.

B<format:no_links>:
Usually, for every format that is specified in a configuration file,
dh_installtex will create a symlink
/usr/bin/I<format> that points to the corresponding engine.  For
example, /usr/bin/latex would be created as a symlink to pdftex.  

The flavor B<format:no_links> inhibits this. If a link already
exists, dh_installtex will skip this format with an informational
message, even if this option is not given.

=back

=head1 NOTES

"dh_installtex" adds a dependency relation onto tex-common to misc:Depends.

Note that this command is not idempotent. "dh_clean -k" should be called
between invocations of this command, unless using the B<-n> option.
Otherwise, it may cause multiple instances of the same text to be added
to maintainer scripts.

Please refer to the Debian TeX policy for details about fonts configuration
for TeX by Debian packages.

=head1 SEE ALSO

L<debhelper(7)>

=head1 AUTHOR

This program and its documentation was written by
Norbert Preining for the Debian Operating System
and both are licensed under the GNU General Public License Version 2 or later.

=cut

### Local Variables:
### perl-indent-level: 4
### tab-width: 4
### indent-tabs-mode: t
### End:
# vim:set tabstop=4 autoindent fileencoding=utf-8: #
